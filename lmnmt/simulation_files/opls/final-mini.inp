* Minimize PDB
*

! read topology and parameter files
stream toppar.str

bomlev -1 !! The sum of charges from LigParGen is not an integer
read psf card name @run/@prot-@lig-neutralized.psf
bomlev 0
read coor card name @run/@prot-@lig-neutralized.crd

! set up SHAKE again
shake bonh param sele all end

! set up crystal and images
set greaterval = @box
crystal define cubic @greaterval @greaterval @greaterval 90. 90. 90.
crystal build noper 0

! These segments are used for water and ions in bulk solvent
define bulks sele segid bwat .or. segid pot .or. segid cl .or. segid sod .or. -
  segid cal .or. segid mg .or. segid cla end

! You may need to change these depending on how you plan to do recentering
image byseg sele .not. resname tip3 .and. .not. bulks end
image byres sele resname tip3 .or. bulks end


!! LigParGen charges doesn't add up to be perfect integers so, I'm skipping this
!!===============================================
!! the total charge on the system must be 0 or else Ewald will not work correctly, we need to test for this...
!! we do the almost equal if test to avoid floating point rounding errors.
scalar charge stat sele all end
!if ?stot .ae. 0 then goto okewald

!! uh-oh, total charge != 0, we cannot proceed, set the bomlev to an absurdly high level
!! to force a bailout.
!bomlev 5
!Achtung, particle-mesh ewald only works correctly on a structure with 0 total charge!!!!

!label okewald
!!=============================================

! set up nonbond correctly to the same values we will be using in dynamics
nbond inbfrq -1 imgfrq -1 -
      elec ewald pmew fftx @fft ffty @fft fftz @fft kappa .34 spline order 6 -
      vdw vfswitch cutnb 16. cutim 16. ctofnb 12. ctonnb 10.0 e14fac 0.5

energy

mini sd nstep 200
mini abnr nstep 1000 nprint 100 tolg 0.01

coor rms
ioform extended

write coor card name @run/@prot-@lig-finalmini.crd
* Final minimized coordinates, done with PME
*

write coor pdb name @run/@prot-@lig-finalmini.pdb

stop

