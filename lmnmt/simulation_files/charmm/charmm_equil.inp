* Molecular Dynamics
* NPT equilibration

set engine = omm

! read topology and parameter files
bomlev -2
stream toppar.str
bomlev 0

read psf card name @run/@prot-@lig-neutralized.psf
read coor card name @run/@prot-@lig-heat.crd


!!-------------- CUBIC CRYSTAL AND IMAGES -------------------!!
! SETUP CRYSTAL (DEFINE, BUILD), IMAGE CENTERING W. MODIFIED PSF
! WE USE THE SAME PARAMS AS IN SOLVATION
set greaterval = @box
crystal define cubic @greaterval @greaterval @greaterval 90. 90. 90.
crystal build noper 0

! These segments are used by CHARMMing for water and ions in bulk solvent
define bulks sele segid bwat .or. segid pot .or. segid cl .or. segid sod .or. -
  segid cal .or. segid mg .or. segid ces end

coor stat

if @engine .eq. omm then
  calc boxhalf = @box / 2
else
 set boxhalf = 0
endif

! Set up images -- center the protein by segment and the solvent by residue
coor translate xdir @boxhalf ydir @boxhalf zdir @boxhalf
coor stat
image byseg xcen @boxhalf ycen @boxhalf zcen @boxhalf sele .not. resname tip3 .and. .not. bulks end ! protein and ligand
image byres xcen @boxhalf ycen @boxhalf zcen @boxhalf sele resname tip3 .or. bulks end

!!---------------------- DCD / RESTART FILES ----------------------!!
open unit 41 write card name @run/@prot-@lig-equil.res !restart file that will be written
open unit 51 read card name @run/@prot-@lig-heat.res !restart file that will be reading
open unit 31 write file name @run/@prot-@lig-equil.dcd !trajectory file name


!!-------------------- nonbonded options PME and VSWITCH/VFSWITCH --------------!!
! set up nonbond parameters -- same as for heating
!  turn on faster options and set-up SHAKE
faster on
nbond inbfrq -1 imgfrq -1 atom vatom cdie eps 1.0 -
    elec ewald pmew fftx @fft ffty @fft  fftz @fft  kappa .34 spline order 6 -
    vdw vswitch cutnb 16.0 cutim 16.0 ctofnb 12.0 ctonnb 10.0
energy

shake fast bonh tol 1.0e-5 para

open write form unit 28 name @run/output_equil
outu 28

if @boxhalf eq 0 then
  set resttoken = restart
  set iurest = 51
else
  set resttoken = start
  set iurest = -1
endif

set nsteps = 2500000

calc time = (@nsteps * 0.002) / 1000  ! ns

!!------------------- DYNAMICS ------------------!!
!!!!!!!!!! Constant temperature / isobaric - MC !!!!!!!!!!!!!!!
! Run NPT ensemble
if @engine .eq. blade then
scalar fbeta set 0.1 sele all end
set ntrfrq = 0 
else
scalar fbeta set 10.0 sele all end
set ntrfrq = 500
endif

if @engine .eq. domdec then
domdec gpu only dlb off ndir 1 1 1
endif

calc pmass = ?natom * 0.12

if @engine .eq. blade then
set dyna = blade prmc iprs 25 pref 1 prdv 100
else
set dyna = omm langevin gamma 20 prmc pref 1 iprsfrq 25 ! turn on openmm, set-up Langevin, set-up MC barostat at 1 atm, move attempt / 25 steps
endif

dynamics cpt leap @resttoken timestep 0.002 nstep @nsteps -
     nprint 5000 iprfrq 5000 isvfrq 5000 - ! iprfrq and ntrfrq are related
     iunwri 41 iunrea @iurest iuncrd 31 nsavcc 4000 -
     firstt 298 finalt 298 -
     ichecw 0 ihtfrq 0 ieqfrq 0 -
     iasors 1 iasvel 1 iscvel 0  -
     ilbfrq 0 inbfrq -1 imgfrq -1 echeck -1 -
     ntrfrq @ntrfrq - ! To conserve center of mass translation
     pconstant pmass @pmass pref 1.0 -
     pgamma 20.0 - ! pressure collision frequenc
     hoover tmass 1000 -
     @dyna

ioform extended

write coor card name @run/@prot-@lig-equil.crd
* coordinates after the equilibration run
*


stop

