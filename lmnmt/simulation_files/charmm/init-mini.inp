* Minimize PDB
*

! read topology and parameter file
bomlev -2
prnlev 1
stream toppar.str
bomlev 0
prnlev 5

! read the psf and coordinate file
read psf card name @run/@prot-@lig.psf
read coor card name @run/@prot-@lig.crd

! set up shake
shake bonh param sele all end

! set up electrostatics, since we're not using PME, set up switching
! electrostatics
nbond inbfrq -1 elec fswitch vdw vswitch cutnb 16. ctofnb 12. ctonnb 10.

energy

coor copy comp

cons harm abso force 10.0 mass select (segid PROT .or. segid COF) show end
mini sd nstep 200 nprint 10 step 0.005
cons harm clear

mini abnr nstep 1000 nprint 100 tolg 0.01
coor rms

ioform extended

write coor card name @run/@prot-@lig-initialmini.crd
* Initial minimization, no PME.
*

write coor pdb name @run/@prot-@lig-initialmini.pdb

stop

