* CHARMM input file for Multi-Site lambda-dynamics
* MSLD references:
*    J.L. Knight and C.L. Brooks III. Multi-site lambda-dynamics: 
*       free energy method for simulating Structure-Activity Relationship 
*       studies. J. Chem. Theory Comput. 7, 2728-2739 (2011). 
*    J.L. Knight and C.L. Brooks III. Applying efficient 
*       implicit non-geometric constraints in free energy simulations.
*       J. Comput. Chem. 32, 3423-3432 (2011). 
* Stream file for MSLD created by process.py
* Written by Ryan Hayes 2016-06-18
*

if ?blade .ne. 1 then
   echo "Test NOT performed. Blade must be defined in pref.dat"
   stop
endif
if ?domdec .ne. 1 then
   echo "Test NOT performed. DOMDEC must be defined in pref.dat"
   stop
endif

set fnex = 5.5
set temp = 298.15

stream "variables@{IFLAT}.inp"

bomblev -1

stream "prep/@{SYSNAME}.inp"

! Set nonbonded options
stream ../nbond.str

faster on

! system "mkdir -p res dcd"

if @{IFLAT} .eq. 1 then
    cons harm abso force 10.0 mass select (segid PROT .or. segid LIG .or. segid LIG1) show end comp
    mini sd nstep 200 nprint 10 step 0.005
    cons harm clear

    ! release restraints and minimize everything
    mini sd nstep 250 nprint 10 step 0.005                  !! Do min, Steepest-Decent

    write psf card name ../prep/minimized.psf 
    * after minimization psf
    *
    write coor card name ../prep/minimized.crd 
    * after minimization crd
    *
    write coor pdb form name ../prep/minimized.pdb 
    * after minimization pdb
    *
    bomblev 0
else
    read coor card name prep/minimized.crd
endif


!---------------------------------------------------------------------------
! Setup of dynamics run
!---------------------------------------------------------------------------

if @nreps .eq. 1 then
   set myrep = 0
else
   !! Initiate BP-REX MSLD
   set dph = 0.5
   calc sph = 7.0 - @dph*@ncentral
   repd fast nrep @nreps EXLM FREQuency 1000 MSPH SPH @sph DPH @dph unit 27

   !! Direct output of replicas to their individual files
   set myrep = ?myrep
endif
open write form unit 28 name output_@myrep
outu 28

stream "../nbshift/vb.inp"

shake fast bonh param
scalar fbeta set 0.1 sele all end

domdec gpu only dlb off ndir 1 @nnodes 1

!! Heating run of 100 ps
open unit 21 write unform name "dcd/@{SYSNAME}_heat.dcd_@MYREP"
open unit 22 write form name "res/@{SYSNAME}_heat.res_@MYREP"
open unit 24 write file name "res/@{SYSNAME}_heat.lmd_@MYREP"
if nreps .gt. 1 then
   open write form unit 27 name "@{SYSNAME}_heat.rex_@MYREP"
endif

if @?restartfile eq 0 then
  set resttoken = start
  set iurest = -1
else
  set resttoken = restart
  open unit 23 read form name ../@{RESTARTFILE}_@MYREP
  set iurest = 23
endif

calc seed = @seed
calc pmass = ?natom * 0.12
dynamics cpt @resttoken -
  blade prmc iprs 100 pref 1 prdv 100 -
  iseed @seed @seed @seed @seed -
  timestep 0.002 -      !!timestep interval
  nstep @esteps -         !!no. of steps  (== 200 ps)
  nprint 1000 -         !!freq of printout
  iprfrq 1000 -         !!freq of calc avg/rms energy
  nsavc 1000 -          !!freq of writing coordinates
  nsavl 10 -            !!freq of writing lambda histograms
  isvfrq @esteps - ! 10000 -        !!freq of writing out restart files
  iunread @iurest -          !!read restart file
  iuncrd 21 -           !!write out coordinates
  iunwri 22 -           !!write out restart file
  iunldm 24 -           !!write out lambda histograms and biasing potential
  firstt @temp -          !!initial temp
  finalt @temp -          !!final temp
  tstruc @temp -          !!temp at which starting structure has been equilibrated
  tbath @temp -           !!temp of langevin bath
  pconstant -           !! use constant pressure (pressure.doc recommendations)
  pmass @pmass -         !! pressure "mass" 2% of mass or 400.0 amu
  pref 1.0 -            !! pressure in atm
  pgamma 20.0 -         !! pressure collision frequency
  hoover -
  reft @temp -
  tmass 1000 -
  ichecw 0 -            !!do not scale velocities to final temp (i.e. equilibrate)
  ihtfrq 0 -            !!freq of heating
  ieqfrq 0 -            !!freq of scaling/assigning velocity(X)
  iasors 1 -            !!assign velocities during heating (0 will be scale velocities)
  iasvel 1 -            !!using gaussian distribution to assign velocities
  iscvel 0 -
  inbfrq -1 -
  ilbfrq 0 -
  imgfrq -1 -
  ntrfrq 0 - ! 500 -
  echeck -1             !!energy tolerance check before crashing

!! Production run of 100 ps / 1 ns
open unit 21 write unform name "dcd/@{SYSNAME}_flat.dcd_@MYREP"
open unit 22 write form name "res/@{SYSNAME}_flat.res_@MYREP"
! open unit 23 read form name "res/@{SYSNAME}_heat.res_@MYREP"
open unit 24 write file name "res/@{SYSNAME}_flat.lmd_@MYREP"
if nreps .gt. 1 then
   open write form unit 27 name "@{SYSNAME}_flat.rex_@MYREP"
endif

calc pmass = ?natom * 0.12
dynamics cpt start -
  blade prmc iprs 100 pref 1 prdv 100 -
  timestep 0.002 -      !!timestep interval
  nstep  @nsteps -          !!no. of steps  (== 2 ns)
  nprint 1000 -         !!freq of printout
  iprfrq 1000 -         !!freq of calc avg/rms energy
  nsavc 1000 -          !!freq of writing coordinates
  nsavl 10 -            !!freq of writing lambda histograms
  isvfrq @nsteps - ! 10000 -        !!freq of writing out restart files
  iunread -1 - ! 23 -          !!read restart file
  iuncrd 21 -           !!write out coordinates
  iunwri 22 -           !!write out restart file
  iunldm 24 -           !!write out lambda histograms and biasing potential
  firstt @temp -          !!initial temp
  finalt @temp -          !!final temp
  tstruc @temp -          !!temp at which starting structure has been equilibrated
  tbath @temp -           !!temp of langevin bath
  pconstant -           !! use constant pressure (pressure.doc recommendations)
  pmass @pmass -         !! pressure "mass" 2% of mass or 400.0 amu
  pref 1.0 -            !! pressure in atm
  pgamma 20.0 -         !! pressure collision frequency
  hoover -
  reft @temp -
  tmass 1000 -
  ichecw 0 -            !!do not scale velocities to final temp (i.e. equilibrate)
  ihtfrq 0 -            !!freq of heating
  ieqfrq 0 -            !!freq of scaling/assigning velocity(X)
  iasors 1 -            !!assign velocities during heating (0 will be scale velocities)
  iasvel 1 -            !!using gaussian distribution to assign velocities
  iscvel 0 -
  inbfrq -1 -
  ilbfrq 0 -
  imgfrq -1 -
  ntrfrq 0 - ! 500 -
  echeck -1             !!energy tolerance check before crashing

!----------------------------------------------------------------------------
! Write out structure after simulation
!----------------------------------------------------------------------------

NOE
   RESET
END
cons harm clear

!write psf card form name "@{SYSNAME}_flat.psf_@MYREP"
!* after dynamics psf
!*
write coor pdb form name "@{SYSNAME}_flat.pdb_@MYREP"
* after dynamics pdb
*

!---------------------------------------------------------------------------
! Analyze l-dynamics output
!---------------------------------------------------------------------------
open unit 44 read file name "res/@{SYSNAME}_flat.lmd_@MYREP"

traj lamb print ctlo 0.95 cthi 0.99 first 44 nunit 1
!traj lamb print ctlo 0.8 cthi 0.95 first 44 nunit 1

stop

